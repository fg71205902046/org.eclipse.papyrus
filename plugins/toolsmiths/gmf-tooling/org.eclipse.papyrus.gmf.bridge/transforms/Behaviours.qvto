/*******************************************************************************
 * Copyright (c) 2011, 2020 Montages A.G., CEA LIST, Artal
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-2.0/ 
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors: 
 * 	Guillaume Hillairet (Montages A.G.) : initial implementation
 *    Aurelien Didier (ARTAL) - aurelien.didier51@gmail.com - Bug 569174
*/
library Behaviours;

modeltype GMFMAP uses mappings('http://www.eclipse.org/papyrus/gmf/2020/mappings');
modeltype GMFGEN uses gmfgen('http://www.eclipse.org/papyrus/gmf/2020/GenModel');
modeltype GMFGRAPH uses gmfgraph('http://www.eclipse.org/papyrus/gmf/2020/GraphicalDefinition');
modeltype ECORE uses ecore('http://www.eclipse.org/emf/2002/Ecore');

--
--	Custom Behaviour
--

helper GMFMAP::CanvasMapping::handleRelatedDiagram(in mapElement : GMFMAP::MappingEntry) : GMFGEN::OpenDiagramBehaviour =
	object GMFGEN::OpenDiagramBehaviour {
		-- XXX alternative approach to tell whether related diagram is the same we are processing now, 
		-- originally self.eResource() != mapElement.eResource()
		if self <> mapElement.container() then {
			diagramKind := 'FIXME put GenEditorGenerator.modelID value here';
			editorID := 'FIXME put GenEditorView.id value here';
		} endif;
	};

--
--	VisualEffect
--

mapping GMFMAP::VisualEffectMapping::createVisualEffects(parent: GMFGEN::GenCommonBase, diagramElement: GMFGRAPH::DiagramElement): GMFGEN::GenVisualEffect {
	name := self.diagramPin.name;
	pinKind := self.diagramPin.oclAsType(EObject).eClass().name;
	operationName := diagramElement.getOperationName(self.diagramPin);
	operationType := self.diagramPin.getOperationType();
	oclExpression := self.oclExpression;
	
	editPolicyQualifiedClassName := parent.getEditPolicyQualifiedClassName(self.diagramPin);
	key := result.editPolicyQualifiedClassName + '.KEY';
}

--
--	Helpers
--

helper GMFGEN::GenCommonBase::getEditPolicyQualifiedClassName(pin: Pin): String =
	self.getDiagram().editPoliciesPackageName + '.' + 
		pin.name.firstToUpper() + // FIXME Should be valid Java Class Name
		self.visualID.toString() + 'Policy';

helper GMFGRAPH::DiagramElement::getOperationName(pin: GMFGRAPH::Pin): String =
	if not self.figure.actualFigure.hasFigurePin(pin) then
		let graphChildAccess = self.figure.accessors->any(figure.hasFigurePin(pin)) in
		if graphChildAccess.oclIsUndefined() then
			pin.getOperationName()	
		else
			 graphChildAccess.accessor + "()." + pin.getOperationName()
		endif 
	else pin.getOperationName() endif;

helper GMFGRAPH::Figure::hasFigurePin(pin: GMFGRAPH::Pin): Boolean =
	let realFigure = 
		if self.oclIsTypeOf(FigureRef) then 
			self.oclAsType(FigureRef).figure 
		else self.oclAsType(RealFigure) endif
	in 
		realFigure.pins->includes(pin);
