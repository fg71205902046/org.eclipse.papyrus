/*******************************************************************************
 * Copyright (c) 2009, 2020 Borland Software Corporation, CEA LIST, Artal
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-2.0/ 
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors: 
 *    Artem Tikhomirov (Borland) - initial API and implementation
 *    Aurelien Didier (ARTAL) - aurelien.didier51@gmail.com - Bug 569174
 */
modeltype GMFMAP uses mappings('http://www.eclipse.org/papyrus/gmf/2020/mappings');
modeltype GMFGEN uses gmfgen('http://www.eclipse.org/papyrus/gmf/2020/GenModel');

transformation PropertySheet(in mapModel : GMFMAP, inout gmfgenModel : GMFGEN);

main() {
	var mapRoot := mapModel.rootObjects()![GMFMAP::Mapping];
	var genEditor := gmfgenModel.rootObjects()![GMFGEN::GenEditorGenerator];
	genEditor.propertySheet := mapRoot.diagram.map propertySheet(genEditor);
}

mapping GMFMAP::CanvasMapping::propertySheet(editorGen : GMFGEN::GenEditorGenerator) : GMFGEN::GenPropertySheet {
	result.tabs += object GMFGEN::GenStandardPropertyTab { iD := 'appearance' };
	result.tabs += object GMFGEN::GenStandardPropertyTab { iD := 'diagram' };
	if not self.domainModel.oclIsUndefined() then 
		result.tabs += object GMFGEN::GenCustomPropertyTab {
			iD := 'domain';
			label := 'Core';
			filter := object GMFGEN::TypeTabFilter {
				types += Sequence { 'org.eclipse.gmf.runtime.notation.View', 'org.eclipse.gef.EditPart' };
				if not editorGen.navigator.oclIsUndefined() then generatedTypes += GMFGEN::GeneratedType::abstractNavigatorItem endif; 
			}
		}
	endif;
}