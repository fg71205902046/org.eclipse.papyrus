/******************************************************************************
 * Copyright (c) 2007, 2020 Borland Software Corporation, CEA LIST, Artal and others
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-2.0/ 
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors: 
 *    Alexander Shatalin (Borland) - initial API and implementation
 *    Michael Golubev (Borland) - [243151] explicit source/target for links
 *    Aurelien Didier (ARTAL) - aurelien.didier51@gmail.com - Bug 569174
 *****************************************************************************/
import xpt.GenModelUtils;

modeltype gmfgen uses "http://www.eclipse.org/papyrus/gmf/2020/GenModel";
modeltype genmodel uses "http://www.eclipse.org/emf/2002/GenModel";

library LinkUtils;

helper gmfgen::GenLink::isTypeLink() : Boolean {
	return not self.modelFacet.oclIsUndefined() and self.modelFacet.oclIsKindOf(gmfgen::TypeLinkModelFacet)
}

helper gmfgen::GenLink::isRefLink() : Boolean {
	return not self.modelFacet.oclIsUndefined() and self.modelFacet.oclIsKindOf(gmfgen::FeatureLinkModelFacet)
}

helper canBeSource(link : gmfgen::GenLink, linkEnd : gmfgen::GenLinkEnd) : Boolean {
	return link.sources->includes(linkEnd)
}

helper canBeTarget(link : gmfgen::GenLink, linkEnd : gmfgen::GenLinkEnd) : Boolean {
	return link.targets->includes(linkEnd)
}

helper canBeContainer(link : gmfgen::GenLink, metaClass : genmodel::GenClass) : Boolean {
	return null <> link.modelFacet and isCompatible(link.modelFacet.getContainerClass(), metaClass)
}
-- MODIFIERS: private
helper gmfgen::LinkModelFacet::getContainerClass() : genmodel::GenClass {
	return null
}
-- MODIFIERS: private
helper gmfgen::TypeLinkModelFacet::getContainerClass() : genmodel::GenClass {
	return self.childMetaFeature.genClass
}
-- MODIFIERS: private
helper gmfgen::FeatureLinkModelFacet::getContainerClass() : genmodel::GenClass {
	return self.getSourceType()
}

-- MODIFIERS: private
helper isCompatible(desiredType : genmodel::GenClass, actualClass : genmodel::GenClass) : Boolean {
	return null <> desiredType and desiredType.isSuperTypeOf(actualClass)
}
